// Module 7 - Time Series Analysis 2
// Demo 1 - Time Series Decomposition

// Connect to the sample database
#connect cluster('help.kusto.windows.net').database('Samples')

let min_t = datetime(2017-01-05);
let max_t = datetime(2017-02-03 22:00);
let dt = 2h;
demo_make_series2
  | make-series Traffic=avg(num) 
             on TimeStamp 
           from min_t to max_t step dt 
             by sid 
    // select a single time series for a cleaner visualization
  | where sid == 'TS1'   
    // decomposition of a set of time series to 
    // seasonal, trend, residual, and baseline (seasonal+trend)
  | extend (baseline, seasonal, trend, residual) = series_decompose(Traffic, -1, 'linefit')  
  | render timechart 
      with (title='Web app. traffic of a month, decomposition', ysplit=panels)
